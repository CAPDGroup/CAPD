
###########
# AC_PREFIX_DEFAULT(/usr/local/)

AC_CONFIG_MACRO_DIRS(capdMake_PATH[/m4])

AC_LANG([C++])
AC_ARG_WITH([top_capd],
	[AS_HELP_STRING([--with-top_capd=<path>],
           [path to top capd directory @<:@default=../@:>@])],
        [top_capd=$withval],
	[top_capd=${default_top_capd}])

AC_SUBST(top_capd)

capdMake=${PWD}/${top_capd}/capdMake
capd_src_dir=${PWD}/${top_capd}
capd_build_dir=${PWD}/${top_capd_build}

AC_SUBST(capdMake)
AC_SUBST(capd_src_dir)
AC_SUBST(capd_build_dir)

# AC_CHECK_FILE Does not work with cross-compilation
# AC_CHECK_FILE([${capdMake}], [], [
# 			     AC_MSG_ERROR([could not find capdMake in ${top_capd}. Set --top_capd to a direcotry where you have capdMake. There we will put binaries and libraries])
# 			     exit -1])

pkg_config_dev="${PWD}/${top_capd_build}/lib/pkgconfig-dev"
pkg_config_install="${PWD}/${top_capd_build}/lib/pkgconfig-install"


if test ! -d ${pkg_config_dev}; then
   mkdir -p ${pkg_config_dev}
fi

if test ! -d ${pkg_config_install}; then
   mkdir -p ${pkg_config_install}
fi


pkg_config_dev=`cd ${pkg_config_dev}; pwd`
pkg_config_install=`cd ${pkg_config_install}; pwd`


# for ${!} syntax check http://wiki.bash-hackers.org/syntax/pe#indirection
AS_IF([ test "x${MXE_PKG_CONFIG_PATH_NAME}" != "x" ],
      [
      export ${MXE_PKG_CONFIG_PATH_NAME}="${pkg_config_dev}${PATH_SEPARATOR}${!MXE_PKG_CONFIG_PATH_NAME}"
      ],
      [
      AS_IF([ test "x${PKG_CONFIG_PATH}" = "x" ],
      [ export PKG_CONFIG_PATH="${pkg_config_dev}"],
      [ export PKG_CONFIG_PATH="${pkg_config_dev}${PATH_SEPARATOR}${PKG_CONFIG_PATH}"])
      ])


AC_SUBST(PKG_CONFIG_DIR,[\${libdir}/pkgconfig])
AC_SUBST(PKG_CONFIG_DEV_DIR,[${pkg_config_dev}])




#
# !!!!!!!!!
#
# Our own AM_SANITY_CHECK. Generated by autoreconf is too slow (with sleep 1 - what for?).
#
# AM_SANITY_CHECK
# ---------------
AC_DEFUN([AM_SANITY_CHECK],
[AC_MSG_CHECKING([whether build environment is sane (CAPD implementation)])

#
# Our implementation do not sleep and if it is a problem, then sleep as in original version.
#

echo timestamp > conftest.file
# Reject unsafe characters in $srcdir or the absolute working directory
# name.  Accept space and tab only in the latter.
am_lf='
'
case `pwd` in
  *[[\\\"\#\$\&\'\`$am_lf]]*)
    AC_MSG_ERROR([unsafe absolute working directory name]);;
esac
case $srcdir in
  *[[\\\"\#\$\&\'\`$am_lf\ \	]]*)
    AC_MSG_ERROR([unsafe srcdir value: `$srcdir']);;
esac

# Do `set' in a subshell so we don't clobber the current shell's
# arguments.  Must try -L first in case configure is actually a
# symlink; some systems play weird games with the mod time of symlinks
# (eg FreeBSD returns the mod time of the symlink's containing
# directory).
if (
   set X `ls -Lt "$srcdir/configure" conftest.file 2> /dev/null`
   if test "$[*]" = "X"; then
      # -L didn't work.
      set X `ls -t "$srcdir/configure" conftest.file`
   fi
   rm -f conftest.file
   if test "$[*]" != "X $srcdir/configure conftest.file" \
      && test "$[*]" != "X conftest.file $srcdir/configure"; then

      # If neither matched, then we have a broken ls.  This can happen
      # if, for instance, CONFIG_SHELL is bash and it inherits a
      # broken ls alias from the environment.  This has actually
      # happened.  Such a system could not be considered "sane".
      AC_MSG_WARN([ls -t appears to fail.  Make sure there is not a broken
alias in your environment])
   fi

   test "$[2]" = conftest.file
   )
then
   # Ok.
   :
else
   AC_MSG_WARN([newly created file is older than distributed files!
Check your system clock])


sleep 1
echo timestamp > conftest.file

if (
   set X `ls -Lt "$srcdir/configure" conftest.file 2> /dev/null`
   if test "$[*]" = "X"; then
      # -L didn't work.
      set X `ls -t "$srcdir/configure" conftest.file`
   fi
   rm -f conftest.file
   if test "$[*]" != "X $srcdir/configure conftest.file" \
      && test "$[*]" != "X conftest.file $srcdir/configure"; then

      # If neither matched, then we have a broken ls.  This can happen
      # if, for instance, CONFIG_SHELL is bash and it inherits a
      # broken ls alias from the environment.  This has actually
      # happened.  Such a system could not be considered "sane".
      AC_MSG_ERROR([ls -t appears to fail.  Make sure there is not a broken
alias in your environment])
   fi

   test "$[2]" = conftest.file
   )
then
   # Ok.
   :
else
   AC_MSG_ERROR([newly created file is older than distributed files!
Check your system clock])
fi


fi
AC_MSG_RESULT(yes)])


# do not add dist-zip because it requires zip command. To create zip archive use
# make dist  DIST_TARGETS=dist-zip
# With an old automake we cannot create only tar.gz (DIST_TARGETS=dist-gzip) when this dist-zip is set
m4_define([capd_automake_init_options], [foreign no-define subdir-objects tar-pax])

AM_PROG_CC_C_O

AM_SILENT_RULES([yes])

CAPD_VERSION_INFO=CAPD_LIBRARY_VERSION_NUMBER
AC_SUBST([CAPD_VERSION_INFO])

if test -e "${capdMake}/../.svn" || test -e "${capdMake}/../.git"; then
   if test "${enable_maintainer_mode+set}" != "set"; then
      enable_maintainer_mode=yes
   fi
   CAPD_REPOS=yes
fi

AM_MAINTAINER_MODE



# Default, for the syntax check manual for AC_PROG_CXX (http://stackoverflow.com/questions/3116645/default-compiler-flags-with-autotools)
# for debug an user should make -g explicit during configure or make
: ${CFLAGS="-g0 -O2"}
: ${CXXFLAGS="-g0 -O2"}

if test "x${CAPD_REPOS}" = "xyes"; then
   CAPD_AM_CXXFLAGS="${CAPD_AM_CXXFLAGS} -g0 -Wall -Wextra"
   CAPD_AM_CPPFLAGS="${CAPD_AM_CPPFLAGS} -g0 -Wall -Wextra"
else
   CAPD_AM_CXXFLAGS="${CAPD_AM_CXXFLAGS} -g0 -O2"
   CAPD_AM_CPPFLAGS="${CAPD_AM_CPPFLAGS} -g0 -O2"
fi

AM_CONDITIONAL([WITH_CAPD_EXAMPLES], [ ! test -n "$WITHOUT_CAPD_EXAMPLES" ])

AX_CXX_COMPILE_STDCXX_17([noext],[optional])
if test "${HAVE_CXX17}" != "1"; then
    AX_CXX_COMPILE_STDCXX_14([noext],[mandatory])
fi

AC_ARG_ENABLE(asserts,
AS_HELP_STRING([--enable-asserts],
               [enable asserts, default: yes if .svn or .git]),
[case "${enableval}" in
             yes) asserts=true ;;
             no)  asserts=false ;;
             *)   AC_MSG_ERROR([bad value ${enableval} for --enable-asserts]) ;;
esac],
[
  AS_IF([test "x${CAPD_REPOS}" = "xyes"],
        [asserts=true],
        [asserts=false])
])

AS_IF([test x"$asserts" = x"false"],
[
   CAPD_AM_CXXFLAGS="${CAPD_AM_CXXFLAGS} -DBOOST_DISABLE_ASSERTS"
])





AC_DEFUN([CAPD_CXX_FLAG_CHECK],
[dnl
  AC_MSG_CHECKING([if $CXX supports $1])
  AC_LANG_PUSH([C++])
  ac_saved_cxxflags="$CXXFLAGS"
  CXXFLAGS="-Werror $1"
  AC_COMPILE_IFELSE([AC_LANG_PROGRAM([])],
    [
    AC_MSG_RESULT([yes])
    CXX_FLAG_$2="$1"
    AC_SUBST([CXX_FLAG_$2])
    ],
    [
    CXX_FLAG_$2=""
    AC_MSG_WARN([no])
    ]
  )
  CXXFLAGS="$ac_saved_cxxflags"
  AC_LANG_POP([C++])
  AM_CONDITIONAL([CXX_FLAG_$2], [ test "x${CXX_FLAG_$2}"!=x ])
])

AC_DEFUN([CAPD_LD_FLAG_CHECK],
[dnl
  AC_MSG_CHECKING([if $LD supports $1])
  AC_LANG_PUSH([C++])
  ac_saved_ldflags="$LDFLAGS"
  LDFLAGS="-Werror $1"
  AC_LINK_IFELSE([AC_LANG_PROGRAM([])],
    [
    AC_MSG_RESULT([yes])
    LD_FLAG_$2="$1"
    AC_SUBST([LD_FLAG_$2])
    ],
    [
    LD_FLAG_$2=""
    AC_MSG_WARN([no])
    ]
  )
  LDFLAGS="$ac_saved_ldflags"
  AC_LANG_POP([C++])
  AM_CONDITIONAL([LD_FLAG_$2], [ test "x${LD_FLAG_$2}"!=x ])
])



CAPD_CXX_FLAG_CHECK([-fdata-sections -ffunction-sections], [capd_fsections])
CAPD_AM_CXXFLAGS="${CAPD_AM_CXXFLAGS} ${CXX_FLAG_capd_fsections}"

CAPD_LD_FLAG_CHECK([-Wl,--gc-sections], [capd_gc_sections])
CAPD_AM_LDFLAGS="${CAPD_AM_LDFLAGS} ${LD_FLAG_capd_gc_sections}"

CAPD_LD_FLAG_CHECK([-Wl,-dead_strip], [capd_dead_strip])

AM_COND_IF([LD_FLAG_capd_gc_sections],
   [],
   [
   CAPD_AM_LDFLAGS="${CAPD_AM_LDFLAGS} ${LD_FLAG_capd_dead_strip}"
   ])


AC_SUBST([CAPD_AM_CXXFLAGS], ["${CAPD_AM_CXXFLAGS}"])
AC_SUBST([CAPD_AM_CPPFLAGS], ["${CAPD_AM_CPPFLAGS}"])
AC_SUBST([CAPD_AM_LDFLAGS], ["${CAPD_AM_LDFLAGS}"])

AC_SUBST([LIBCAPD_LDFLAGS], ["-Wl,-Bstatic"])


AC_SUBST([AM_CXXFLAGS], ["-I\$(top_builddir)/include -I\$(top_srcdir)/include -DFILE_BUILD_DIR=\\\"\$(abs_builddir)\\\" \${CAPD_AM_CXXFLAGS}"])
AC_SUBST([AM_CPPFLAGS], ["-I\$(top_builddir)/include -I\$(top_srcdir)/include -DFILE_BUILD_DIR=\\\"\$(abs_builddir)\\\" \${CAPD_AM_CPPFLAGS}"])
AC_SUBST([AM_LDFLAGS], ["\$(CAPD_AM_LDFLAGS)"])


# AC_SUBST([ACLOCAL_AMFLAGS], ["-I\$(capdMake)/m4 -I m4"])

#########
